begin parameters

	#Concentrations
	#Metabolites
	glucose_0	0
	pyruvate_0	0
	lactate_0	0
	
	#Enzymes
	ldh_0		1000
	pc_0		1000
	pepck_0		1000	
	eno_0		1000	
	pgm_0		1000
	pgk_0		1000	
	gapdh_0		1000	
	aldo_0		1000
	fbp_0		1000
	gpi_0		1000	
	g6pc_0		1000				
	#hk_0		1000
	#pfk_0		1000
	#pkm2_0		1000

	#Rates
	kbf			1e-6
	kbr			1e-1
	kf			1e-1

end parameters

begin molecule types

	#Enzymes
	LDH(bind,active~N~Y)
	PC(bind,active~N~Y)
	PEPCK(bind,active~N~Y)
	ENO(bind,active~N~Y)
	PGM(bind,active~N~Y)
	PGK(bind,active~N~Y)
	GAPDH(bind,active~N~Y)
	ALDO(bind,active~N~Y)	
	FBP(bind,active~N~Y)
	GPI(bind,active~N~Y)	
	G6PC(bind,active~N~Y)
	#HK(bind,active~N~Y)
	#PFK(bind,active~N~Y)
	#PKM2(bind,active~N~Y)
	 
	#Metabolites
	Lactate(bind)
	Pyruvate(bind)
	Oxaloacetate(bind)
	PEP(bind)
	PG2(bind)	
	PG3(bind)
	BP13G(bind)
	GADP(bind)	
	F16BP(bind)
	F6P(bind)	
	G6P(bind)						
	Glucose(bind)
		
end molecule types

begin seed species

	#Enzymes
	LDH(bind,active~Y)		ldh_0
	PC(bind,active~Y)		pc_0
	PEPCK(bind,active~Y)	pepck_0
	ENO(bind,active~Y)		eno_0	
	PGM(bind,active~Y)		pgm_0
	PGK(bind,active~Y)		pgk_0
	GAPDH(bind,active~Y)	gapdh_0
	ALDO(bind,active~Y)		aldo_0
	FBP(bind,active~Y)		fbp_0
	GPI(bind,active~Y)		gpi_0
	G6PC(bind,active~Y)		g6pc_0
	#HK(bind,active~Y)		hk_0
	#PFK(bind,active~Y)		pfk_0
	#PKM2(bind,active~Y)	pkm2_0

	#Metabolites
	Pyruvate(bind)			pyruvate_0
	Glucose(bind)			glucose_0
	Lactate(bind)			lactate_0
	
end seed species

begin observables

	Molecules	glucose		Glucose()
	Molecules	g6p			G6P()
	Molecules	f6p			F6P()
	Molecules	f16bp		F16BP()
	Molecules	gadp		GADP()
	Molecules	bpg13		BP13G()
	Molecules	pg3			PG3()
	Molecules	pg2			PG2()
	Molecules	pep			PEP()
	Molecules	pyruvate	Pyruvate()
	Molecules	lactate		Lactate()
	
end observables

begin reaction rules 

LDH(bind,active~Y) + Lactate(bind) <-> LDH(bind!1,active~Y).Lactate(bind!1)	kbf,kbr
LDH(bind!1,active~Y).Lactate(bind!1) -> LDH(bind,active~Y) + Pyruvate(bind)	kf

PC(bind,active~Y) + Pyruvate(bind) <-> PC(bind!1,active~Y).Pyruvate(bind!1)	kbf,kbr
PC(bind!1,active~Y).Pyruvate(bind!1) -> PC(bind,active~Y) + Oxaloacetate(bind)	kf

PEPCK(bind,active~Y) + Oxaloacetate(bind) <-> PEPCK(bind!1,active~Y).Oxaloacetate(bind!1)	kbf,kbr
PEPCK(bind!1,active~Y).Oxaloacetate(bind!1) -> PEPCK(bind,active~Y) + PEP(bind)	kf

ENO(bind,active~Y) + PEP(bind) <-> ENO(bind!1,active~Y).PEP(bind!1)	kbf,kbr
ENO(bind!1,active~Y).PEP(bind!1) -> ENO(bind,active~Y) + PG2(bind)	kf

PGM(bind,active~Y) + PG2(bind) <-> PGM(bind!1,active~Y).PG2(bind!1)	kbf,kbr
PGM(bind!1,active~Y).PG2(bind!1) -> PGM(bind,active~Y) + PG3(bind)	kf

PGK(bind,active~Y) + PG3(bind) <-> PGK(bind!1,active~Y).PG3(bind!1)	kbf,kbr
PGK(bind!1,active~Y).PG3(bind!1) -> PGK(bind,active~Y) + BP13G(bind)	kf

GAPDH(bind,active~Y) + BP13G(bind) <-> GAPDH(bind!1,active~Y).BP13G(bind!1)	kbf,kbr
GAPDH(bind!1,active~Y).BP13G(bind!1) -> GAPDH(bind,active~Y) + GADP(bind)	kf

ALDO(bind,active~Y) + GADP(bind) <-> ALDO(bind!1,active~Y).GADP(bind!1)	kbf,kbr
ALDO(bind!1,active~Y).GADP(bind!1) -> ALDO(bind,active~Y) + F16BP(bind)	kf

FBP(bind,active~Y) + F16BP(bind) <-> FBP(bind!1,active~Y).F16BP(bind!1)	kbf,kbr
FBP(bind!1,active~Y).F16BP(bind!1) -> FBP(bind,active~Y) + F6P(bind)	kf

GPI(bind,active~Y) + F6P(bind) <-> GPI(bind!1,active~Y).F6P(bind!1)	kbf,kbr
GPI(bind!1,active~Y).F6P(bind!1) -> GPI(bind,active~Y) + G6P(bind)	kf

G6PC(bind,active~Y) + G6P(bind) <-> G6PC(bind!1,active~Y).G6P(bind!1)	kbf,kbr
G6PC(bind!1,active~Y).G6P(bind!1) -> G6PC(bind,active~Y) + Glucose(bind)	kf

end reaction rules

#Actions
visualize({type=>"contactmap"})
generate_network({})

#equilibrate
setConcentration("Glucose(bind)",0)
setConcentration("Pyruvate(bind)",0)
simulate_ode({suffix=>"equil",t_start=>0,t_end=>1e6,n_steps=>1000})

#simulate
#setConcentration("Glucose(bind)",500)
#setConcentration("Pyruvate(bind)",500)
setConcentration("Lactate(bind)",500)
simulate_ode({t_start=>0,t_end=>12*60*60,n_steps=>500})

